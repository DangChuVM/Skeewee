def secrets = System.getenv("GOOGLE_SERVICES_JSON")
def analyticsEnabled = secrets != null

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

if (analyticsEnabled) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

android {
    compileSdk 34

    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def baseVersion = "v6.4.0-SNAPSHOT-" + getCommitHash()

    defaultConfig {
        applicationId "com.sketchware.remod"
        //noinspection ExpiredTargetSdkVersion since we don't target getting Sketchware Pro on Google Play.
        targetSdkVersion 28
        versionCode 150
        versionName baseVersion

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        buildConfigField("String", "FLAVOR_NAME_WITH_AABS", "\"minApi26\"")
        buildConfigField("String", "FLAVOR_NAME_WITHOUT_AABS", "\"minApi21\"")

        buildConfigField("String", "VERSION_NAME_WITHOUT_FLAVOR", "\"${baseVersion}\"")

        buildConfigField("String", "CRASH_REPORT_WEBHOOK_URL", "\"${System.getenv("CRASH_REPORT_WEBHOOK_URL")}\"")
    }

    flavorDimensions = ["api"]
    productFlavors {
        minApi26 {
            dimension "api"
            minSdkVersion 26
            versionNameSuffix "-minApi26"
            isDefault = true
        }

        minApi21 {
            dimension "api"
            minSdkVersion 21
            versionNameSuffix "-minApi21"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        resources.excludes += "license/*"
        resources.excludes += "META-INF/DEPENDENCIES"
        resources.pickFirsts += "api_database/*"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    androidResources {
        additionalParameters.addAll("--stable-ids", "public-stable-ids.txt")
    }

    signingConfigs {
        debug {
            storeFile file("../testkey.keystore")
            storePassword "testkey"
            keyAlias "testkey"
            keyPassword "testkey"
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    configurations.minApi26Implementation {
        exclude group: 'javax.inject', module: 'javax.inject'
    }

    namespace "com.sketchware.remod"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"], exclude: "proguard-base-7.2.2.jar")

    implementation libs.androidx.appcompat
    implementation libs.androidx.core.splashscreen
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.cardview
    implementation libs.androidx.viewpager
    implementation libs.androidx.swiperefreshlayout
    implementation libs.material
    implementation libs.lottie
    implementation libs.flexbox

    implementation libs.androidx.annotation
    implementation libs.annotations
    implementation libs.guava
    implementation libs.androidx.exifinterface
    implementation libs.androidx.preference.ktx

    implementation platform(libs.editor.bom)
    implementation libs.editor
    implementation libs.language.java
    implementation libs.language.textmate

    implementation libs.circleimageview
    implementation libs.filepicker

    minApi26Implementation libs.bundletool
    //noinspection GradleDependency copied from above dependency for not just runtime access
    minApi26Implementation libs.protobuf.java
    //noinspection GradleDependency
    implementation "com.android.tools:sdklib:25.3.0", { exclude group: "com.intellij", module: "annotations" }
    implementation libs.r8
    implementation libs.zipalign.java

    implementation libs.gson
    implementation libs.scpkix.jdk15on

    //noinspection GradleDependency
    implementation libs.okhttp
    //noinspection GradleDependency
    implementation libs.okio
    implementation libs.dependencyresolver

    minApi21Implementation libs.proguard.base
    minApi26Implementation files('libs/proguard-base-7.2.2.jar')
    minApi26Implementation libs.proguard.core
    minApi26Implementation libs.kotlin.stdlib.jdk8

    //noinspection GradleDependency
    minApi21Implementation libs.ecj
    minApi26Implementation libs.ecj.v3260

    minApi26Implementation libs.kotlinc.android

    implementation libs.core
    implementation libs.insetter

    // Analytics and Crashlytics
    if (analyticsEnabled) {
        new File("$projectDir/google-services.json").text = secrets
    }
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.crashlytics
    implementation libs.firebase.analytics

    coreLibraryDesugaring libs.desugar.jdk.libs.nio
}