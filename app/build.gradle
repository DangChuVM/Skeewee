def secrets = System.getenv("GOOGLE_SERVICES_JSON")
def analyticsEnabled = secrets != null

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
if (analyticsEnabled) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

android {
    compileSdk 36

    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()
    def shortCommitHash = "git rev-parse --short HEAD".execute().text.trim()

    defaultConfig {
        applicationId "pro.sketchware"
        namespace "pro.sketchware"
        targetSdkVersion 28
        minSdkVersion 26
        versionCode 150
        versionName "v7.0.0-SNAPSHOT-" + shortCommitHash

        buildConfigField("String", "GIT_HASH",                    "\"${gitCommitHash}\"")
        buildConfigField("String", "FLAVOR_NAME_WITH_AABS",       "\"minApi26\"")
        buildConfigField("String", "FLAVOR_NAME_WITHOUT_AABS",    "\"minApi21\"")
        buildConfigField("String", "VERSION_NAME_WITHOUT_FLAVOR", "\"v7.0.0-SNAPSHOT-${shortCommitHash}\"")
        buildConfigField("String", "SKETCHUB_API_KEY",            "\"${System.getenv("SKETCHUB_API_KEY")}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        resources.excludes += "license/*"
        resources.excludes += "META-INF/DEPENDENCIES"
        resources.pickFirsts += "api_database/*"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    androidResources {
        additionalParameters.addAll("--stable-ids", "${rootDir}/public-stable-ids.txt")
    }

    signingConfigs {
        debug {
            storeFile file("../testkey.keystore")
            storePassword "testkey"
            keyAlias "testkey"
            keyPassword "testkey"
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    configurations.implementation {
        exclude group: 'javax.inject', module: 'javax.inject'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"], exclude: "proguard-base-7.2.2.jar")

    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "androidx.activity:activity:1.10.0-beta01"
    implementation "androidx.core:core-splashscreen:1.1.0-rc01"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"
    implementation "androidx.recyclerview:recyclerview:1.4.0-beta01"
    implementation "androidx.preference:preference:1.2.1"
    implementation "androidx.viewpager2:viewpager2:1.1.0"
implementation "androidx.viewpager:viewpager:1.1.0"
    implementation "androidx.exifinterface:exifinterface:1.4.0-beta01"
    implementation "androidx.cardview:cardview:1.0.0"

    implementation "com.google.android.material:material:1.14.0-alpha02"
    implementation "com.airbnb.android:lottie:6.5.2"

    implementation "com.github.bumptech.glide:glide:5.0.0-rc01"
    implementation "io.coil-kt:coil:2.7.0"
    implementation "io.coil-kt:coil-svg:2.7.0"

    def soraEditorVersion = "0.23.5"
    implementation platform("io.github.Rosemoe.sora-editor:bom:$soraEditorVersion")
    implementation "io.github.Rosemoe.sora-editor:editor"
    implementation "io.github.Rosemoe.sora-editor:language-java"
    implementation "io.github.Rosemoe.sora-editor:language-textmate"

    implementation "com.google.code.gson:gson:2.11.0"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "com.squareup.okio:okio:3.9.0"
    implementation "io.noties.markwon:core:4.6.2"

    implementation "com.android.tools.build:bundletool:1.16.0"
    implementation "com.android.tools:sdklib:31.4.1"

    implementation "com.android.tools:r8:8.9.35"
    implementation "org.eclipse.jdt:ecj:3.39.0"
    implementation "com.github.Iyxan23:zipalign-java:1.2.2"
    implementation "com.guardsquare:proguard-core:9.1.4"
    implementation "org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r"
    implementation "org.slf4j:slf4j-nop:2.0.13"

    implementation "com.github.topjohnwu.libsu:core:5.2.2"
    implementation "com.madgag:scpkix-jdk15on:1.47.0.2"
    implementation "com.google.android.flexbox:flexbox:3.0.0"

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "dev.chrisbanes.insetter:insetter:0.6.1"
    implementation files('libs/android-svg.jar')

    implementation "com.github.Cosmic-Ide:DependencyResolver:290880eb6c"
    implementation "com.github.Cosmic-IDE:kotlinc-android:f49c67a8c0"
    implementation "com.github.PranavPurwar:filepicker:74526bea66"

    implementation platform("com.google.firebase:firebase-bom:33.1.1")
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    if (analyticsEnabled) {
        new File("$projectDir/google-services.json").text = secrets
    }
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs_nio:2.2.2"
}